description = 'Spring Social Wunderlist'

group = "org.springframework.social"

repositories {
    mavenCentral()
}

apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "maven"

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies{
    compile ("org.springframework.social:spring-social-core:$springSocialVersion")
    compile ("org.springframework:spring-web:$springVersion")
    compile ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    compile ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    compile ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")

    testCompile ("junit:junit:$junitVersion")
    testCompile ("com.jayway.jsonpath:json-path:$jsonPathVersion")
    testCompile ("org.mockito:mockito-core:$mockitoVersion")
    testCompile ("org.springframework:spring-test:$springVersion")
    testCompile ("org.springframework.social:spring-social-test:$springSocialTestVersion")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacoco-html"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
